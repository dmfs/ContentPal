// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath 'org.jacoco:org.jacoco.core:0.8.8'
        classpath 'digital.wup:android-maven-publish:3.6.2'
    }
}
plugins {
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0' apply false
}

def gitVersion = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}


allprojects {
    group 'org.dmfs'
    version gitVersion()
    repositories {
        mavenCentral()
        google()
    }
}


if (project.hasProperty('SONATYPE_USERNAME') && project.hasProperty('SONATYPE_PASSWORD')) {
    apply plugin: 'io.github.gradle-nexus.publish-plugin'

    nexusPublishing {
        repositories {
            sonatype {
                username = SONATYPE_USERNAME
                password = SONATYPE_PASSWORD
                nexusUrl.set(uri("https://oss.sonatype.org/service/local/"))
                snapshotRepositoryUrl.set(uri("https://oss.sonatype.org/content/repositories/snapshots/"))
            }
        }
    }
}

apply from: "dependencies.gradle"

task clean(type: Delete) {
    delete rootProject.buildDir
}
